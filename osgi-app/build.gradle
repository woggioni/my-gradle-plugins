plugins {
    id "java-gradle-plugin"
}

childProjects.forEach {name, child ->
    child.with {
        apply plugin: 'maven-publish'

        publishing {
            repositories {
                maven {
                    url = woggioniMavenRepositoryUrl
                }
            }
            publications {
                maven(MavenPublication) {
                    from(components["java"])
                }
            }
        }
    }
}

evaluationDependsOnChildren()

configurations {
    embedded {
        transitive = false
        visible = false
        canBeConsumed = false
    }
}

dependencies {
    embedded project(path: "osgi-simple-bootstrapper", configuration: 'tar')
    embedded project(path: "osgi-simple-bootstrapper-api")
    embedded project(path: "osgi-simple-bootstrapper-application")

    implementation catalog.bndlib
    implementation group: 'biz.aQute.bnd', name: 'biz.aQute.bnd.gradle', version: catalog.versions.bnd.get()
}

jar {
    into("META-INF") {
        from(configurations.embedded)
    }
}

gradlePlugin {
    plugins {
        osgiAppPlugin {
            id = 'net.woggioni.gradle.osgi-app'
            implementationClass = 'net.woggioni.gradle.osgi.app.OsgiAppPlugin'
        }
    }
}
